# Maintainer: En Shih <seanstone5923@gmail.com>
_target=riscv32-unknown-elf
_gitname=riscv-gcc
pkgname=${_target}-gcc-git
pkgver=r161737.be9abee2aaa
pkgrel=1
pkgdesc="The GNU Compiler Collection (${_target})"
arch=('x86_64')
url="https://github.com/riscv/${_gitname}"
license=('GPL')
groups=("${_target}-toolchain")
depends=('libmpc' 'zlib' "${_target}-binutils" "${_target}-newlib")
makedepends=('git' 'gmp' 'mpfr')
provides=("${_target}-gcc")
conflicts=("${_target}-gcc")
source=("git+https://github.com/riscv/${_gitname}.git")
md5sums=('SKIP')
options=('!emptydirs' '!strip')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
}

build() {
    cd "$srcdir/$_gitname"
    mkdir -p build && cd build
    ../configure \
        --prefix=/usr \
        --target=${_target} \
        --with-sysroot=/usr/${_target} \
        --libdir=/usr/lib \
        --libexecdir=/usr/lib \
		--disable-shared \
		--disable-threads \
		--enable-languages=c,c++ \
		--enable-tls \
		--with-newlib \
		--with-native-system-header-dir=/include \
		--disable-libmudflap \
		--disable-libssp \
		--disable-libquadmath \
		--disable-libgomp \
		--disable-nls \
        --with-arch=rv32imc
    make
}

package() {
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install
    rm -r $pkgdir/usr/lib/*.so*
    rm -rf $pkgdir/usr/share
}
