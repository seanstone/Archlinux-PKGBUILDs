# Maintainer: En Shih <seanstone5923@gmail.com>
_target=vc4-elf
_gitname=newlib-vc4
pkgname=${_target}-newlib-git
pkgver=r3904.89abe4a5
pkgrel=1
pkgdesc="A C library intended for use on embedded systems (${_target})"
arch=('any')
url="https://github.com/itszor/${_gitname}"
license=('GPL')
groups=("${_target}-toolchain")
depends=()
makedepends=('git' "${_target}-gcc-stage1")
provides=("${_target}-newlib")
conflicts=("${_target}-newlib")
source=("git+https://github.com/itszor/${_gitname}.git#branch=vc4")
md5sums=('SKIP')
options=('!strip' '!emptydirs')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"
}

build() {
    cd "$srcdir/$_gitname"
    mkdir -p build && cd build
    ../configure --prefix=/usr --target=${_target}
    make
}

package() {
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install
}
