# Maintainer: En Shih <seanstone5923@gmail.com>
_target=pru
_gitname=gcc
_commit=c45bebb5ee928eacafa4c408e499bf4f51388cf3
pkgname=${_target}-gcc-stage1
pkgver=c45bebb5ee928eacafa4c408e499bf4f51388cf3
pkgrel=1
pkgdesc="The GNU Compiler Collection. Stage 1 for toolchain building (${_target})"
arch=('x86_64')
url="https://github.com/dinuxbg/gnupru"
license=('GPL')
groups=("${_target}-toolchain")
depends=('libmpc' 'zlib' "${_target}-binutils")
makedepends=('git' 'gmp' 'mpfr')
optdepends=("${_target}-newlib: Standard C library optimized for embedded systems")
conflicts=("${_target}-gcc")
source=("https://github.com/gcc-mirror/${_gitname}/archive/${_commit}.tar.gz" "git+https://github.com/dinuxbg/gnupru")
md5sums=('SKIP' 'SKIP')
options=('!emptydirs' '!strip')

pkgver() {
    echo "$_commit"
}

prepare() {
    cd "$srcdir/$_gitname-$_commit"

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    ls "$srcdir/gnupru/patches/${_gitname}" | sort | while read PATCH
    do
        patch -p1 < $srcdir/gnupru/patches/${_gitname}/$PATCH || die "Could not apply patch $PATCH"
    done
}

build() {
    cd "$srcdir/$_gitname-$_commit"
    mkdir -p build && cd build
    ../configure \
        --prefix=/usr \
        --target=${_target} \
        --host=$CHOST \
        --build=$CHOST \
        --with-sysroot=/usr/${_target} \
        --libdir=/usr/lib \
        --libexecdir=/usr/lib \
		--disable-nls \
        --with-newlib \
        --with-bugurl="https://github.com/dinuxbg/gnupru/issues" \
        --without-headers \
        --enable-languages=c
    make all-gcc
}

package() {
    cd "$srcdir/$_gitname-$_commit/build"
    make DESTDIR="$pkgdir" install-gcc
    rm -rf $pkgdir/usr/share
}
