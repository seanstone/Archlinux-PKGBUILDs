# Maintainer: En Shih <seanstone5923@gmail.com>
pkgname=vc4-gcc-git-pass1
pkgver=r146716.e90ff43f967
pkgrel=1
pkgdesc="Port of GCC to the VideoCore4 processor."
arch=('x86_64')
url="https://github.com/itszor/gcc-vc4"
license=('GPL')
groups=('vc4-toolchain')
depends=()
makedepends=('git')
provides=('vc4-gcc')
conflicts=('vc4-gcc' 'vc4-gcc-git')
source=('git+https://github.com/itszor/gcc-vc4.git#branch=vc4-gcc6')
md5sums=('SKIP')
options=('!buildflags')

_gitname=gcc-vc4

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"
}

build() {
    cd "$srcdir/$_gitname"
    mkdir -p build && cd build
    ../configure --target=vc4-elf --prefix=/usr --disable-nls \
      --disable-ssp --enable-languages=c --without-headers --with-newlib \
      --disable-decimal-float --disable-threads --disable-libatomic \
      --disable-libitm --disable-libsanitizer --disable-libquadmath \
      --disable-lto --enable-sjlj-exceptions
    make
}

package() {
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install
    rm -r $pkgdir/usr/lib/*.so*
    rm -rf $pkgdir/usr/share
}
