# Maintainer: En Shih <seanstone5923@gmail.com>
_target=pru
_gitname=binutils-gdb
_commit=0930cb3021b8078b34cf216e79eb8608d017864f
pkgname=${_target}-binutils
pkgver=0930cb3021b8078b34cf216e79eb8608d017864f
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (${_target})"
arch=('x86_64')
url="https://github.com/dinuxbg/gnupru"
license=('GPL')
groups=("${_target}-toolchain")
depends=('zlib')
makedepends=('git')
source=("https://github.com/bminor/${_gitname}/archive/${_commit}.tar.gz" "git+https://github.com/dinuxbg/gnupru")
md5sums=('SKIP' 'SKIP')

pkgver() {
    echo "$_commit"
}

prepare() {
    cd "$srcdir/$_gitname-$_commit"
    
    # Hack - see native package for details
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    ls "$srcdir/gnupru/patches/${_gitname}" | sort | while read PATCH
    do
        patch -p1 < $srcdir/gnupru/patches/${_gitname}/$PATCH || die "Could not apply patch $PATCH"
    done
}

build() {
    cd "$srcdir/$_gitname-$_commit"
    mkdir -p build && cd build
    ../configure \
        --target=${_target} \
        --prefix=/usr \
        --with-sysroot=/usr/${_target} \
		--disable-nls \
		--disable-gdb \
        --with-bugurl="https://github.com/dinuxbg/gnupru/issues"
    make all
}

package() {
    cd "$srcdir/$_gitname-$_commit/build"
    make DESTDIR="$pkgdir" install
    rm -rf $pkgdir/usr/lib
    rm -rf $pkgdir/usr/share/{info,locale}
}
