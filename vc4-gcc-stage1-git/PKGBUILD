# Maintainer: En Shih <seanstone5923@gmail.com>
_target=vc4-elf
_gitname=gcc-vc4
pkgname=${_target}-gcc-stage1-git
pkgver=r146716.e90ff43f967
pkgrel=1
pkgdesc="The GNU Compiler Collection. Stage 1 for toolchain building (${_target})"
arch=('x86_64')
url="https://github.com/itszor/${_gitname}"
license=('GPL')
groups=("${_target}-toolchain")
depends=('libmpc' 'zlib' "${_target}-binutils")
makedepends=('git' 'gmp' 'mpfr')
optdepends=("${_target}-newlib: Standard C library optimized for embedded systems")
provides=("${_target}-gcc-stage1")
conflicts=("${_target}-gcc" "${_target}-gcc-git" "${_target}-gcc-stage1")
source=("git+https://github.com/itszor/${_gitname}.git#branch=vc4-gcc6")
md5sums=('SKIP')
options=('!emptydirs' '!strip')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
}

build() {
    cd "$srcdir/$_gitname"
    mkdir -p build && cd build
    ../configure \
        --prefix=/usr \
        --target=${_target} \
        --host=$CHOST \
        --build=$CHOST \
        --with-sysroot=/usr/${_target} \
        --libdir=/usr/lib \
        --libexecdir=/usr/lib \
        --with-gnu-as \
        --with-gnu-ld \
        --with-newlib \
        --without-headers \
        --enable-languages=c \
        --disable-nls \
        --disable-ssp \
        --disable-decimal-float \
        --disable-threads \
        --disable-libatomic \
        --disable-libitm \
        --disable-libsanitizer \
        --disable-libquadmath \
        --disable-lto \
        --enable-sjlj-exceptions
    make all-gcc
}

package() {
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install-gcc
    rm -rf $pkgdir/usr/share
}
