# Maintainer: En Shih <seanstone5923@gmail.com>
_target=vc4-elf
_gitname=gcc-vc4
pkgname=${_target}-gcc-git
pkgver=r146716.e90ff43f967
pkgrel=1
pkgdesc="The GNU Compiler Collection (${_target})"
arch=('x86_64')
url="https://github.com/itszor/${_gitname}"
license=('GPL')
groups=("${_target}-toolchain")
depends=('libmpc' 'zlib' "${_target}-binutils" "${_target}-newlib")
makedepends=('git' 'gmp' 'mpfr')
provides=("${_target}-gcc")
conflicts=("${_target}-gcc")
source=("git+https://github.com/itszor/${_gitname}.git#branch=vc4-gcc6")
md5sums=('SKIP')
options=('!emptydirs' '!strip')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
}

build() {
    cd "$srcdir/$_gitname"
    mkdir -p build && cd build
    ../configure \
        --prefix=/usr \
        --target=${_target} \
        --with-sysroot=/usr/${_target} \
        --libdir=/usr/lib \
        --libexecdir=/usr/lib \
        --with-gnu-as \
        --with-gnu-ld \
        --with-python-dir=share/gcc-${_target} \
        --with-newlib \
        --without-included-gettext \
        --enable-checking=release \
        --disable-nls \
        --disable-ssp \
        --enable-languages=c,c++ \
        --disable-decimal-float \
        --enable-threads \
        --disable-libatomic \
        --disable-libsanitizer \
        --disable-libquadmath \
        --disable-lto \
        --enable-sjlj-exceptions
    make
}

package() {
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install
    rm -r $pkgdir/usr/lib/*.so*
    rm -rf $pkgdir/usr/share
}
