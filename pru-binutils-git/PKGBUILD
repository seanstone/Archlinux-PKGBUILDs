# Maintainer: En Shih <seanstone5923@gmail.com>
_target=pru
_gitname=binutils-gdb
pkgname=${_target}-binutils-git
pkgver=r95786.7fb4aca842
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (${_target})"
arch=('x86_64')
url="https://github.com/dinuxbg/gnupru"
license=('GPL')
groups=("${_target}-toolchain")
depends=('zlib')
makedepends=('git')
provides=("${_target}-binutils")
conflicts=("${_target}-binutils")
source=("git+https://github.com/bminor/${_gitname}.git#commit=0930cb3021b8078b34cf216e79eb8608d017864f" "git+https://github.com/dinuxbg/gnupru")
md5sums=('SKIP' 'SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"
    # Hack - see native package for details
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    ls "$srcdir/gnupru/patches/binutils-gdb" | sort | while read PATCH
    do
        git am -3 < $srcdir/gnupru/patches/binutils-gdb/$PATCH || die "Could not apply patch $PATCH"
    done
}

build() {
    cd "$srcdir/$_gitname"
    mkdir -p build && cd build
    ../configure \
        --target=${_target} \
        --prefix=/usr \
        --with-sysroot=/usr/${_target} \
		--disable-nls \
		--disable-gdb \
        --with-bugurl="https://github.com/dinuxbg/gnupru/issues"
    make all
}

package() {
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install
    rm -rf $pkgdir/usr/lib
    rm -rf $pkgdir/usr/share/{info,locale}
}
